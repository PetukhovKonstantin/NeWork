import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.9.24"
    ext.hilt_version = "2.51.1"
    ext.android_gradle_version = '8.5.2'
    ext.google_services_version = '4.4.2'

    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.google.gms.google-services' version "$google_services_version" apply false
    id 'com.android.application' version "$android_gradle_version" apply false
    id 'com.android.library' version "$android_gradle_version" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.dagger.hilt.android' version "$hilt_version" apply false
}

subprojects {
    tasks.withType(KotlinCompile).tap {
        configureEach {
            kotlinOptions.jvmTarget = "17"
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

ext {
    mapkitApiKey = getMapkitApiKey()
    serverApiKey = getServerApiKey()
}

private String getMapkitApiKey() {
    def properties = new Properties()
    project.file("local.properties").withInputStream { properties.load(it) }
    return properties.getProperty("MAPKIT_API_KEY", "")
}

private String getServerApiKey() {
    def properties = new Properties()
    project.file("local.properties").withInputStream { properties.load(it) }
    return properties.getProperty("SERVER_API_KEY", "")
}